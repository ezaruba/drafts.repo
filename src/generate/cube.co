request = require \request

getColor = (colors) ->
  {length} = colors
  if length is 0
    \A
  else if length is 1
    colors.0
  else
    \Y

normal = (packs) ->
  for pack, i of packs
    pack = for card of pack
      o = card{
        score: cube_average
        url: image_medium
        name
        rarity
      } <<<<
        cmc: card.mana_cost_converted or 0
        color: getColor card.effective_cost
      o
    pack

module.exports = (cubeName, count, cb) ->
  options =
    uri: "http://tappedout.net/api/collection/cube/#cubeName/booster/?count=#count"
    json: true
  request options, (err, res, body) ->
    if err or res.statusCode is not 200
      cb "error fetching #cubeName: #{err?code or res.statusCode}"
    else
      try
        normalized = normal body
      catch
        err = "error with #cubeName: search the tappedout page for 'unknown' and remove those cards, or use a different cube"
      cb err, normalized
