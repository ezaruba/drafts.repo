module.exports = _ =
  at: (arr, index) ->
    {length} = arr
    index = (index + length) % length # negative modulo
    arr[index]
  choose: (n, arr) ->
    i = arr.length
    end = i - n or 1
    while i > end
      j = _.rand i--
      arr[i, j] = arr[j, i]
    arr.slice -n
  flat: -> []concat.apply [] it
  rand: -> Math.floor Math.random! * it
  shuffle: -> _.choose it.length, it
  uid: -> _.rand 9e9 .toString 16
  without: (arr, vals) ->
    unless vals instanceof Array
      vals = [vals]
    for el of arr
      unless ~vals.indexOf el
        el
