{EventEmitter} = require \events
{rand} = require \./_

class Player extends EventEmitter
  ->
    @ <<<<
      picks: []
      packs: []

  time: 0

  startRound: ->
    @picks.push @round = []

  receive: (pack) ->
    @packs.push pack

    if @packs.length is 1
      @sendPack!

  autopick: ->
    index = rand @packs.0.length
    @pick index, true

  pick: (index, autopick) ->
    return unless card = @packs.0?[index]
    pack = @packs.shift!
    pack.splice index, 1

    if @name is not \bot
      @cards.push card
    @round.push { card.name, autopick }

    if @packs.length
      @sendPack!
    else
      @time = 0

    @emit \pass pack, @index

    true

module.exports = Player
